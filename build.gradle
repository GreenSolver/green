plugins {
	id 'application'
	id 'java'
	id 'eclipse'
	id 'de.fuerstenau.buildconfig' version '1.1.8'
	id 'maven-publish'
}

mainClassName = 'za.ac.sun.cs.green.Green'
group = 'za.ac.sun.cs'

def getLongVersionName = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'describe', '--tags'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

def getShortVersionName = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'describe', '--tags', '--abbrev=0'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

version = getLongVersionName()

repositories {
	mavenCentral()
	jcenter()
}

buildConfig {
	sourceSets {
		main {
			packageName = 'za.ac.sun.cs.green'
		}
  	}
}

dependencies {
	compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.4'
	compile group: 'org.apache.commons', name: 'commons-exec', version: '1.3'
	compile group: 'org.apfloat', name: 'apfloat', version: '1.8.2'
	compile group: 'redis.clients', name: 'jedis', version: '2.4.2'
	compile group: 'org.choco-solver', name: 'choco-solver', version: '4.0.6'
	compile group: 'org.sosy-lab', name: 'javasmt-solver-z3', version: 'z3-4.4.1-1558-gf96cfea'
	testCompile group: 'junit', name: 'junit', version: '4.12'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			version getShortVersionName() + '-SNAPSHOT'
		}
	}
	repositories {
		maven {
			credentials {
				username project.getProperty('greenRepoUsername')
				password project.getProperty('greenRepoPassword')
			}
			url project.getProperty('greenRepoAddress')
		}
	}
}
